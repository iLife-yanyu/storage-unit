plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

ext {
    appVersionInt = 4
    appVersionString = "1.0.4." + appVersionInt
    min_sdk = 26
    target_sdk = 34
}

android {
    namespace 'com.yanyu.demoapp'
    compileSdk target_sdk

    defaultConfig {
        applicationId "com.yanyu.demoapp"
        minSdk min_sdk
        targetSdk target_sdk
        versionCode appVersionInt
        versionName appVersionString

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                // 指定 Room Migration 升级数据库导出的 Schema 文件位置
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        main {
            res.srcDirs 'src/main/res', 'src/main/res-appIcon'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->

            // 重命名
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '.apk'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity-ktx:1.8.0'
    implementation project(':storage-unit')
}